;======================================================================
; setup.ins NOTEPAD++
; Installer type: NSIS
; 
; J. Boettge <boettge@mpi-halle.mpg.de>  2020-07-20 08:17:08 +0200
;======================================================================

;======================================================================
[Initial]
;======================================================================
setLogLevel=5
; Log Errors in Logfile and abort:
ExitOnError=true
; Show syntax errors in the script:
ScriptErrorMessages=off
; Dont trace step by step through the script:
TraceMode=off 
; Let started programs run in front of the winst window
StayOnTop=false 

;======================================================================
[Actions]
;======================================================================
include_insert "%ScriptPath%\product_variables.opsiinc"

if $isDFN$ = "false"
;=== Check free space
if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	;=== remove previous installation
	if FileExists("%ScriptPath%\delsub.opsiinc")
		comment  "Starting uninstall sub section"
		; sub "%ScriptPath%\delsub.opsiinc"	
		include_append "%ScriptPath%\delsub.opsiinc"
		sub_delsub_main
	else
		logError "Can't find uninstaller script"
		isFatalError "delsub.opsiinc not found"
	endif	
	
	; Start the installation
	ShowBitmap $Img_Install$ $ProductName$
	Message "Installing " + $ProductName$ + " " + $Version$+ " (" + $sw_arch$ + " bit)"
	
	marktime	
	
	if not (FileExists($Installer$))
		logError "Installer not found: [" + $Installer$ + "]"
		isFatalError
	endif
	
	comment "Start setup program"
	Switch $InstallerType$
		Case "MSI"
			Winbatch_install_msi
			Sub_check_exitcode_msi
		EndCase
		Case "INNO"
			Winbatch_install_inno
			Sub_check_exitcode_inno
		EndCase
		Case "NSIS"
			Winbatch_install_nsis
			Sub_check_exitcode_nsis
		EndCase	
		DefaultCase
			logError "Unsupported insaller type: [" + $InstallerType$ + "]"
			isFatalError			
		EndCase	
	EndSwitch
	
	set $MainBin$ = $InstallDir$+$CheckBin$
	
	if not (FileExists($MainBin$))
		logError "Main binary not found! [" + $InstallDir$+$MainBin$ + "]"
		isFatalError
	endif
	
	
	;=== extract language files form installer
	if (FileExists($LocaleDir$))
		ShowBitmap $Img_Plugins$ $ProductName$
		ShellInAnIcon_extract_languages
		set $ExitCode$ = GetLastExitCode
		if not($ExitCode$ = "0")
			logError "Error " + $ExitCode$ + " while extracting language files from installer."
		endif
	else
		logError "Target directory for language files does not exist"
	endif
	
	
	;=== configure application
	ShowBitmap $Img_Config$ $ProductName$
	
	;=== create desktop link if requested
	if ($pr_DesktopLink$ = "true")
		LinkFolder_CreateDesktopLink
	endif


	;=== Create entries in start menu
	if (FileExists("%CommonProgramsDir%\" + $ProductName$))
		LinkFolder_clear_StartMenuLinks
	endif
	;LinkFolder_create_StartMenuLinks
	
	
	;=== select application language
	comment "Set application language:"
	if $sw_lang$ = "auto"
		Switch $System_Language$
			Case "0407"
				comment "    detected language: German"
				Set $LangSourceFile$ = "german.xml"
			EndCase
			Case "0409"
				comment "    detected language: English"
				Set $LangSourceFile$ = "english.xml"
			EndCase
			DefaultCase
				comment "    detected language code is " + $System_Language$ + "; using default (english)"
				Set $LangSourceFile$ = "english.xml"
			EndCase
		endif
		EndSwitch
	else
		Switch $sw_lang$
			Case "de"
				comment "    selected language by property: German"
				Set $LangSourceFile$ = "german.xml"
			EndCase
			Case "en"
				comment "    selected language by property: English"
				Set $LangSourceFile$ = "english.xml"
			EndCase
		EndSwitch
	endif
	comment "    ...selection of 'nativeLang.xml'"
	Files_copy_lang

	;=== restore plugins if required
	if ($ActionRequest$="setup") and ($pr_KeepPlugins$= "True")
		if FileExists($TmpDir$) and ($plugin_count$ INT> "0")
			Message "Restoring installed plugins..."
			if FileExists($PluginDir$)
				; copy plugins:
				for %x% in $Plugins$ do sub_restore_plugin
				; checking targets:
				Set $PluginsTarget$ = shellCall('dir "' + $PluginDir$ + '" /b /a:d')
				set $PluginsTarget$ = getSubListByMatch ($Plugins$, $PluginsTarget$)
				set $plugin_count_target$ = count($PluginsTarget$)
				if ($plugin_count_target$ = $plugin_count$)
					comment "All plugins copied. Removing temporary folder"
					; defined in delsub.opsiinc:
					files_deleteTemp
				else
					logError "Found " + $plugin_count_target$ + " copied plugin directories. " + $plugin_count$ + " were expected."
				endif
			else
				logError "Target directory for plugins does not exist. Skipping restore."
			endif
		else
			Comment "No saved plugins found. Nothing to do."
		endif

	endif

	;=== use updater?
	if ($pr_Updater$ = "no")
		comment "Disable auto update:"
		; delete directory 'updater' to remove menu entries under "Help"
		comment "   Remove updater directory"
		Files_uninstall_updater
	endif
	
	;=== custom post install
	comment "include custom post install file"
	if not ($CustomPostInstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
			ShowBitmap $Img_Install_Custom$ $ProductName$
			include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
			ShowBitmap $Img_Install$ $ProductName$
		endif
	endif
	
	;=== measure installation time
	set $TimeDiff$ = getDiffTimeSec
	Message  "Setup time: " + $TimeDiff$ + " seconds"	
endif
endif

;=== Prepare the change from dfn_ to o4i_
; Stage 1 - logWarning
; Stage 2 - logError (entire functionality)
; Stage 3 - logError (NO functionality)
; Stage 4 - logError (NO functionality except uninstall; no binaries) 
;           removal of this package from repository pending
;-----------------------------------------
; This is stage 4
;-----------------------------------------
if $isDFN$ = "true"
	logError "*******************************************************************************************"
	logError "This package will not be maintained any longer!"
	logError "The functionality is completely taken over by o4i_" + $StrippedProductId$+"."
	logError "Please install o4i_" + $StrippedProductId$ + " from the repository."
	logError "dfn_" + $StrippedProductId$ + " should be removed the then from your depot."
	logError "*******************************************************************************************"
	; set $result$ = "Package is of life"
	; set $result$ = currentProductSetReport($result$)
	isFatalError "Discontinued package!"
endif


 
;======================================================================
[Winbatch_install_msi]
;======================================================================
$MSI_Inst_Cmd$ $MSI_Inst_Opts$  
 
;======================================================================
[Winbatch_install_inno]
;======================================================================
$Installer$ $INNO_Inst_Opts$ 
 
;======================================================================
[Winbatch_install_nsis]
;======================================================================
$Installer$ $NSIS_Inst_Opts$
 
;======================================================================
[ShellInAnIcon_extract_languages]
;======================================================================
"$SEVENZIP$" e -o"$LocaleDir$" "$Installer$" -y "*\nppLocalization\*.xml"
exit %ERRORLEVEL%

;====================================================================== 
[ShellInAnIcon_copy_default_config]
;======================================================================
copy /Y "%ScriptPath%\custom\$CustomConfig$"  "$InstallDir$$DefaultXML$"
 
 
;=====================================================================
[LinkFolder_clear_StartMenuLinks]
;=====================================================================
set_basefolder common_programs
;set_subfolder ""
;delete_element "$StrippedProductId$"
set_subfolder "$ProductName$"
delete_element "Uninstall $ProductName$"
 
;=====================================================================
[LinkFolder_CreateDesktopLink]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: $ProductName$
       target: "$MainBin$"
       parameters:
       working_dir: "$InstallDir$"
       icon_file: "$MainBin$"
       icon_index:
end_link


;=====================================================================
[LinkFolder_create_StartMenuLinks]
;=====================================================================
set_basefolder common_programs
set_subfolder "$ProductName$"
set_link 
	name: $ProductName$
	target: "$MainBin$"
	parameters:
	working_dir: "$InstallDir$"
	icon_file:
	icon_index:
end_link


;=====================================================================
[Files_copy_lang]
;=====================================================================
copy "$LocaleDir$$LangSourceFile$" "$InstallDir$"
move "$InstallDir$$LangSourceFile$" "$InstallDir$nativeLang.xml"


;=====================================================================
[Files_uninstall_updater]
;=====================================================================
delete -sf "$InstallDir$\updater"


;=====================================================================
[sub_restore_plugin]
;=====================================================================
if FileExists($tmpDir$+"%x%") 
	files_restorePlugin
endif


;=====================================================================
[files_restorePlugin]
;=====================================================================
copy  -s "$TmpDir$%x%\" "$PluginDir$%x%"
